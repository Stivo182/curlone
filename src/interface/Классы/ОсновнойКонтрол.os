#Использовать "../../core"

Перем КонвертерКомандыCURL;

&Контроллер("/")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

&ТочкаМаршрута("")
&Отображение("./src/interface/view/index.html")
Процедура Главная(Ответ) Экспорт

КонецПроцедуры

&ТочкаМаршрута("/convert")
Процедура Конвертировать(ПараметрыЗапросаИменные, Ответ) Экспорт
	Перем Ошибки;

	Данные = Новый Структура("result, errors", "", Новый Массив());
	
	ТекстКоманды = ПараметрыЗапросаИменные.Получить("cmd");
	Язык = ПараметрыЗапросаИменные.Получить("lang");

	Если ТекстКоманды <> Неопределено Тогда
		Если КонвертерКомандыCURL = Неопределено Тогда
			КонвертерКомандыCURL = Новый КонвертерКомандыCURL();
		КонецЕсли;

		Попытка
			Данные.result = КонвертерКомандыCURL.Конвертировать(ТекстКоманды, ПолучитьГенератор(Язык), Ошибки);

			Для Каждого Ошибка Из Ошибки Цикл
				Данные.errors.Добавить(НоваяОшибка(Ошибка.Текст, Ошибка.Критичная));
			КонецЦикла;
		Исключение
			Данные.errors.Добавить(НоваяОшибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), Истина));
		КонецПопытки;
	КонецЕсли;

	Парсер = Новый ПарсерJSON();

	Ответ.УстановитьТипКонтента("json");
	Ответ.ТелоТекст = Парсер.ЗаписатьJSON(Данные);

КонецПроцедуры

Функция ПолучитьГенератор(Язык)
	Если Язык = "connector" Тогда
		Возврат Новый ГенераторПрограммногоКодаКоннекторHTTP();
	ИначеЕсли Язык = "1c" Тогда
		Возврат Новый ГенераторПрограммногоКода1С();
	Иначе
		Возврат ГенераторПоУмолчанию();
	КонецЕсли;
КонецФункции

Функция ГенераторПоУмолчанию()
	Возврат Новый ГенераторПрограммногоКода1С();
КонецФункции

Функция НоваяОшибка(Текст, Критичная = Ложь)
	Возврат Новый Структура("text, critical", Текст, Критичная);
КонецФункции