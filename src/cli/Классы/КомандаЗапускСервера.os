&Пластилин Перем Настройки;
&Пластилин Перем ЗапускательВебПриложения;

&Опция(Имя = "p port", Описание = "Порт для запуска веб-сервера")
&ТЧисло
Перем Порт;

&Опция(Имя = "o open", Описание = "Открыть в браузере")
&ТБулево
&ПоУмолчанию(Ложь)
Перем ОткрытьВБраузере;

&Деталька("winow.Порт") 
Перем ПортПоУмолчанию;

&КомандаПриложения(Имя = "web", Описание = "Запуск веб-сервера")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

&ВыполнениеКоманды
Процедура Запустить() Экспорт

	Настройки.Порт = Порт();
	
	Если ОткрытьВБраузере Тогда
		ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЗапуститьБраузерСЗадержкой");
	КонецЕсли;

	ЗапускательВебПриложения.Запустить();

КонецПроцедуры

Процедура ЗапуститьБраузерСЗадержкой() Экспорт

	Приостановить(500);

	Адрес = СтрШаблон("http://%1:%2", Настройки.ИмяХоста, Формат(Порт(), "ЧГ=0"));
	ЗапуститьПриложение(Адрес);
	
КонецПроцедуры

Функция Порт()
	Возврат ?(ЗначениеЗаполнено(Порт), Порт, ПортПоУмолчанию);
КонецФункции