#Использовать "../../internal"

Перем ВходящиеАргументы; // Массив из Строка
Перем Генератор; // Объект генератора программного кода

#Область ПрограммныйИнтерфейс

Процедура Конвертировать() Экспорт

	Если Не НайденаКоманда() Тогда
		ВызватьИсключение "Не найдена команда convert";
	КонецЕсли;
	
	ПодготовленныеАргументы = ПодготовитьАргументы();

	Ошибки = Новый Массив();
	КонвертерКомандыCURL = Новый КонвертерКомандыCURL();

	Попытка
		Результат = КонвертерКомандыCURL.Конвертировать(ПодготовленныеАргументы, Генератор, Ошибки);
	Исключение
		Результат = "";
		Ошибки.Добавить(ОбщегоНазначения.НоваяКритичнаяОшибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки;

	Для Каждого Ошибка Из Ошибки Цикл
		Цвет = ?(Ошибка.Критичная, "Красный", "Желтый");
		ЦветнойВывод.Вывести("// ");
		ЦветнойВывод.ВывестиСтроку(Ошибка.Текст, Цвет);
	КонецЦикла;

	Если ЗначениеЗаполнено(Результат) Тогда
		Если Ошибки.Количество() Тогда
			Консоль.Вывести(Символы.ПС);
		КонецЕсли;

		Консоль.Вывести(Результат);
	КонецЕсли;

КонецПроцедуры

Функция НайденаКоманда() Экспорт
	Возврат ВходящиеАргументы.Количество() И НРег(ВходящиеАргументы[0]) = "convert";	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПриСозданииОбъекта(Аргументы)
	ВходящиеАргументы = Аргументы;
КонецПроцедуры

Функция ПодготовитьАргументы()

	ПодготовленныеАргументы = Новый Массив();
	ОбщегоНазначения.ДополнитьМассив(ПодготовленныеАргументы, ВходящиеАргументы);

	ПодготовленныеАргументы[0] = "curl";

	УдалитьПовторяющийАргументCurl(ПодготовленныеАргументы);
	ПрочитатьАргументГенератора(ПодготовленныеАргументы);

	Возврат ПодготовленныеАргументы;

КонецФункции

Процедура УдалитьПовторяющийАргументCurl(Аргументы)
	
	Если Аргументы.Количество() > 1 И НРег(Аргументы[1]) = "curl" Тогда
		Аргументы.Удалить(0);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьАргументГенератора(Аргументы)
	
	ИндексАргумента = 1;

	Если Аргументы.Количество() > ИндексАргумента Тогда
		Аргумент = НРег(Аргументы[ИндексАргумента]);
		Генератор = ПолучитьГенераторПоАргументу(Аргумент);
	КонецЕсли;

	Если Генератор = Неопределено Тогда
		Генератор = ГенераторПоУмолчанию();
	Иначе
		Аргументы.Удалить(ИндексАргумента);
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьГенераторПоАргументу(Аргумент)
	Если Аргумент = "connector" Тогда
		Возврат Новый ГенераторПрограммногоКодаКоннекторHTTP();
	ИначеЕсли Аргумент = "1c" Тогда
		Возврат Новый ГенераторПрограммногоКода1С();
	КонецЕсли;
КонецФункции

Функция ГенераторПоУмолчанию()
	Возврат Новый ГенераторПрограммногоКода1С();
КонецФункции

#КонецОбласти