
#Использовать "../lib/cli/src/core"
#Использовать "internal"
#Использовать autumn
#Использовать autumn-cli
#Использовать winow
#Использовать coloratos
#Использовать "core"
#Использовать "cli"

Процедура Запустить()

	ИнициализацияТекущегоКаталога();

	Если ЕстьКомандаКонвертации() Тогда
		КонвертироватьИВывестиВКонсоль();
	Иначе
		Поделка = Новый Поделка();
		Поделка.ЗапуститьПриложение();
	КонецЕсли;

КонецПроцедуры

Процедура КонвертироватьИВывестиВКонсоль()

	Аргументы = Новый Массив();
	ОбщегоНазначения.ДополнитьМассив(Аргументы, АргументыКоманднойСтроки);
	Аргументы[0] = "curl";

	Если Аргументы.Количество() > 1 
		И Аргументы[1] = "curl" Тогда
		Аргументы.Удалить(0);
	КонецЕсли;

	Ошибки = Новый Массив();
	КонвертерКомандыCURL = Новый КонвертерКомандыCURL();

	Попытка
		Результат = КонвертерКомандыCURL.Конвертировать(Аргументы, Новый ГенераторПрограммногоКода1С(), Ошибки);
	Исключение
		Результат = "";
		Ошибка = Новый Структура("Текст, Критичная", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()), Истина);
		Ошибки.Добавить(Ошибка);
	КонецПопытки;

	Для Каждого Ошибка Из Ошибки Цикл
		Цвет = ?(Ошибка.Критичная, "Красный", "Желтый");
		ЦветнойВывод.Вывести("// ");
		ЦветнойВывод.ВывестиСтроку(Ошибка.Текст, Цвет);
	КонецЦикла;

	Если ЗначениеЗаполнено(Результат) Тогда
		Если Ошибки.Количество() Тогда
			Консоль.Вывести(Символы.ПС);
		КонецЕсли;

		Консоль.Вывести(Результат);
	КонецЕсли;

КонецПроцедуры

Функция ЕстьКомандаКонвертации()
	Возврат АргументыКоманднойСтроки.Количество() 
		И АргументыКоманднойСтроки[0] = "convert";	
КонецФункции

Процедура ИнициализацияТекущегоКаталога()
	ТекущийКаталог = Новый Файл(ОбъединитьПути(ТекущийСценарий().Каталог, "..")).ПолноеИмя;
	УстановитьТекущийКаталог(ТекущийКаталог);
КонецПроцедуры

Запустить();