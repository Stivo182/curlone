#Использовать "../internal"

Перем Результат;
Перем ОписаниеЗапроса;

Перем ИмяПараметраЗаголовки; 
Перем ИмяПараметраHTTPСоединение;
Перем ИмяПараметраЗащищенноеСоединение;
Перем ИмяПараметраHTTPЗапрос;
Перем ИмяПараметраHTTPОтвет;

Перем ПрочитанныеФайлы; // Массив из Структура
                        //   - ПередаваемыйФайл
						//   - ИмяПеременной
Перем ИспользуетсяЗащищенноеСоединение; // Булево

Процедура ПриСозданииОбъекта()

	ИмяПараметраЗаголовки = "Заголовки";
	ИмяПараметраHTTPСоединение = "Соединение";
	ИмяПараметраЗащищенноеСоединение = "ЗащищенноеСоединение";
	ИмяПараметраHTTPЗапрос = "HTTPЗапрос";
	ИмяПараметраHTTPОтвет = "HTTPОтвет";

	ПрочитанныеФайлы = Новый Массив;
	ИспользуетсяЗащищенноеСоединение = Ложь;

КонецПроцедуры

Функция Получить(пОписаниеЗапроса) Экспорт

	ОписаниеЗапроса = пОписаниеЗапроса;

	Результат = "";

	Для Каждого URL Из ОписаниеЗапроса.URL Цикл
		
		СтруктураURL = Новый ПарсерURL(URL);
	
		ВывестиЗаголовки();
		ВывестиЗащищенноеСоединение(СтруктураURL);
		ВывестиHTTPСоединение(СтруктураURL);
		ВывестиЧтениеФайлов();
		ВывестиHTTPЗапрос(СтруктураURL);
		ВывестиУстановкуТелаЗапросаТекстовымиДанными();
		ВывестиУстановкуТелаЗапросаДвоичнымиДанными();
		ВывестиВызовHTTPМетода();

	КонецЦикла;

	Возврат СокрЛП(Результат);

КонецФункции

Функция ПолучитьПорт(СтруктураURL)

	Порт = СтруктураURL.Порт;
	Если Не ЗначениеЗаполнено(Порт) Тогда
		Если СтруктураURL.Схема = "https" Тогда
			Порт = 443;
		Иначе
			Порт = 80;
		КонецЕсли;
	КонецЕсли;

	Возврат Формат(Порт, "ЧГ=");

КонецФункции

Процедура ВывестиЗащищенноеСоединение(СтруктураURL)

	ИспользуетсяЗащищенноеСоединение = СтруктураURL.Схема = "https"
		Или ЗначениеЗаполнено(ОписаниеЗапроса.ИмяФайлаСертификатаКлиента);

	Если Не ИспользуетсяЗащищенноеСоединение Тогда
		Возврат;
	КонецЕсли;

	ВывестиПустуюСтроку();

	// Сертификат клиента
	ИмяПараметраСертификатаКлиента = "";
	Если ЗначениеЗаполнено(ОписаниеЗапроса.ИмяФайлаСертификатаКлиента) Тогда
		ИмяПараметраСертификатаКлиента = "СертификатКлиента";

		ПараметрыФункции = Новый Массив;
		ПараметрыФункции.Добавить(ОбернутьКавычками(ОписаниеЗапроса.ИмяФайлаСертификатаКлиента));
		ПараметрыФункции.Добавить(ОбернутьКавычками(ОписаниеЗапроса.ПарольСертификатаКлиента, Истина));

		ВывестиСтроку(СтрШаблон("%1 = Новый СертификатКлиентаФайл(%2);", 
			ИмяПараметраСертификатаКлиента, 
			ПараметрыФункцииВСтроку(ПараметрыФункции)));
	КонецЕсли;

	// Сертификаты УЦ
	ИмяПараметраСертификатыУдостоверяющихЦентров = "СертификатыУдостоверяющихЦентров";
	Если ОписаниеЗапроса.ИспользоватьСертификатыУЦИзХранилищаОС Тогда
		ВывестиСтроку(СтрШаблон("%1 = Новый СертификатыУдостоверяющихЦентровОС();", ИмяПараметраСертификатыУдостоверяющихЦентров));
	ИначеЕсли ЗначениеЗаполнено(ОписаниеЗапроса.ИмяФайлаСертификатовУЦ) Тогда
		ВывестиСтроку(СтрШаблон("%1 = Новый СертификатыУдостоверяющихЦентровФайл(%2);", 
			ИмяПараметраСертификатыУдостоверяющихЦентров,
			ОбернутьКавычками(ОписаниеЗапроса.ИмяФайлаСертификатовУЦ)));		
	Иначе
		ИмяПараметраСертификатыУдостоверяющихЦентров = "";
	КонецЕсли;

	// Защищенное соединение
	ПараметрыФункции = Новый Массив;
	ПараметрыФункции.Добавить(ИмяПараметраСертификатаКлиента);
	ПараметрыФункции.Добавить(ИмяПараметраСертификатыУдостоверяющихЦентров);
	
	ВывестиСтроку(СтрШаблон("%1 = Новый ЗащищенноеСоединениеOpenSSL(%2);", 
		ИмяПараметраЗащищенноеСоединение,
		ПараметрыФункцииВСтроку(ПараметрыФункции)));
	
КонецПроцедуры

Процедура ВывестиHTTPСоединение(СтруктураURL)

	ПараметрыФункции = Новый Массив;
	ПараметрыФункции.Добавить(ОбернутьКавычками(СтруктураURL.Сервер));
	ПараметрыФункции.Добавить(ПолучитьПорт(СтруктураURL));
	ПараметрыФункции.Добавить(ОбернутьКавычками(ОписаниеЗапроса.ИмяПользователя, Истина));
	ПараметрыФункции.Добавить(ОбернутьКавычками(ОписаниеЗапроса.ПарольПользователя, Истина));
	ПараметрыФункции.Добавить(""); // Прокси
	ПараметрыФункции.Добавить(""); // Таймаут
	ПараметрыФункции.Добавить(?(ИспользуетсяЗащищенноеСоединение, ИмяПараметраЗащищенноеСоединение, ""));

	ВывестиПустуюСтроку();
	ВывестиСтроку(СтрШаблон(ИмяПараметраHTTPСоединение + " = Новый HTTPСоединение(%1);", 
		ПараметрыФункцииВСтроку(ПараметрыФункции)));

КонецПроцедуры

Процедура ВывестиЗаголовки()

	Если ОписаниеЗапроса.Заголовки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ВывестиСтроку(ИмяПараметраЗаголовки + " = Новый Соответствие();");

	Для Каждого Заголовок Из ОписаниеЗапроса.Заголовки Цикл
		СтрокаЗаголовка = СтрШаблон("%1.Вставить(""%2"", ""%3"");", 
			ИмяПараметраЗаголовки, 
			Заголовок.Ключ, 
			ЭкранироватьКавычки(Заголовок.Значение));
		ВывестиСтроку(СтрокаЗаголовка);
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиЧтениеФайлов()

	ПрочитанныеФайлы.Очистить();

	НомерФайла = 1;
	Для Каждого ПередаваемыйФайл Из ОписаниеЗапроса.Файлы Цикл

		Если Не (ПередаваемыйФайл.ПрочитатьСодержимое 
				Или ОписаниеЗапроса.ПередаватьОтправляемыеДанныеВСтрокуЗапроса) Тогда
			Продолжить;
		КонецЕсли;

		ИмяПеременной = "ТекстовыеДанныеИзФайла_" + Формат(НомерФайла, "ЧГ=");

		ВывестиПустуюСтроку();

		Шаблон = "ТекстовыйДокумент = Новый ТекстовыйДокумент();
			|ТекстовыйДокумент.Прочитать(%2);
			|%1 = ТекстовыйДокумент.ПолучитьТекст();";

		Если ПередаваемыйФайл.УдалятьПереносыСтрок Тогда
			Шаблон = Шаблон + "
			|%1 = СтрЗаменить(%1, Символы.ПС, """");
			|%1 = СтрЗаменить(%1, Символы.ВК, """");";
		КонецЕсли;

		Если ПередаваемыйФайл.КодироватьСодержимое Тогда
			Шаблон = Шаблон + "
			|%1 = КодироватьСтроку(%1, СпособКодированияСтроки.URLВКодировкеURL);";
		КонецЕсли;

		Если ЗначениеЗаполнено(ПередаваемыйФайл.Ключ) Тогда
			Шаблон = Шаблон + "
			|%1 = """ + ПередаваемыйФайл.Ключ + "="" + %1;";
		КонецЕсли;

		Текст = СтрШаблон(Шаблон, 
		ИмяПеременной, 
			ОбернутьКавычками(ПередаваемыйФайл.ИмяФайла));

		ВывестиСтроку(Текст);
	
		ПрочитанныйФайл = Новый Структура("ПередаваемыйФайл, ИмяПеременной", ПередаваемыйФайл, ИмяПеременной);
		ПрочитанныеФайлы.Добавить(ПрочитанныйФайл);

		НомерФайла = НомерФайла + 1;
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиHTTPЗапрос(СтруктураURL)

	ПараметрыФункции = Новый Массив;
	
	АдресРесурсаКод = ПолучитьКодСборкиАдресаРесурса(СтруктураURL);
	Если СтрЧислоСтрок(АдресРесурсаКод) > 1 Тогда
		ВывестиПустуюСтроку();
		ВывестиСтроку(СтрШаблон("АдресРесурса = %1;", АдресРесурсаКод));
		ПараметрыФункции.Добавить("АдресРесурса");
	Иначе
		ПараметрыФункции.Добавить(АдресРесурсаКод);
	КонецЕсли;

	Если ОписаниеЗапроса.Заголовки.Количество() Тогда
		ПараметрыФункции.Добавить(ИмяПараметраЗаголовки);
	КонецЕсли;

	ВывестиПустуюСтроку();
	ВывестиСтроку(СтрШаблон(ИмяПараметраHTTPЗапрос + " = Новый HTTPЗапрос(%1);", ПараметрыФункцииВСтроку(ПараметрыФункции)));

КонецПроцедуры

Функция ПолучитьКодСборкиАдресаРесурса(СтруктураURL)

	Кавычка = """";
	РазделительПараметровЗапроса = "&";
	КонкатенацияСПереносомСтрокиИАмперсандом = "
	|	+ ""&"" + ";
	КонкатенацияСПереносомСтроки = "
	|	+ ";

	КавычкаЗакрыта = Ложь;
	Код = Кавычка + СтруктураURL.Путь;
	СтрокаЗапроса = СобратьИсходнуюСтрокуЗапроса(СтруктураURL);

	ПереданныеПараметрыЗапроса = СтрСоединить(ОписаниеЗапроса.ПараметрыЗапроса, РазделительПараметровЗапроса);
	Если ЗначениеЗаполнено(ПереданныеПараметрыЗапроса) Тогда
		СтрокаЗапроса = СтрокаЗапроса 
			+ ?(ЗначениеЗаполнено(СтрокаЗапроса), РазделительПараметровЗапроса, "")
			+ ПереданныеПараметрыЗапроса;
	КонецЕсли;

	Если ОписаниеЗапроса.ПередаватьОтправляемыеДанныеВСтрокуЗапроса Тогда
		ПереданныеПараметрыЗапроса = СтрСоединить(ОписаниеЗапроса.ОтправляемыеТекстовыеДанные, РазделительПараметровЗапроса);

		Если ЗначениеЗаполнено(ПереданныеПараметрыЗапроса) Тогда
			СтрокаЗапроса = СтрокаЗапроса 
				+ ?(ЗначениеЗаполнено(СтрокаЗапроса), РазделительПараметровЗапроса, "")
				+ ПереданныеПараметрыЗапроса;
		КонецЕсли;
	КонецЕсли;

	КодПрочитанныхФайлов = "";	
	Для Каждого ПрочитанныйФайл Из ПрочитанныеФайлы Цикл
		Если ОписаниеЗапроса.ПередаватьОтправляемыеДанныеВСтрокуЗапроса
			Или ПрочитанныйФайл.ПередаваемыйФайл.Назначение = НазначенияФайлов.СтрокаЗапроса Тогда
			КодПрочитанныхФайлов = КодПрочитанныхФайлов 
				+ ?(КодПрочитанныхФайлов = "", "", КонкатенацияСПереносомСтрокиИАмперсандом)
				+ ПрочитанныйФайл.ИмяПеременной;
		КонецЕсли;
	КонецЦикла;

	Если ЗначениеЗаполнено(СтрокаЗапроса) 
		Или ЗначениеЗаполнено(КодПрочитанныхФайлов) Тогда
		Код = Код + "?";
	КонецЕсли;

	Код = Код + СтрокаЗапроса;

	Если ЗначениеЗаполнено(КодПрочитанныхФайлов) Тогда
		Код = Код + Кавычка 
			+ ?(ЗначениеЗаполнено(СтрокаЗапроса), 
				КонкатенацияСПереносомСтрокиИАмперсандом, 
				КонкатенацияСПереносомСтроки) 
			+ КодПрочитанныхФайлов;
		КавычкаЗакрыта = Истина;
	КонецЕсли;

	Если ЗначениеЗаполнено(СтруктураURL.Фрагмент) Тогда
		Фрагмент = "#" + СтруктураURL.Фрагмент;
		Если КавычкаЗакрыта Тогда
			Код = Код + " 
			|	+ " + ОбернутьКавычками(Фрагмент);
		Иначе
			Код = Код + Фрагмент;
		КонецЕсли;
	КонецЕсли;

	Если Не КавычкаЗакрыта Тогда
		Код = Код + Кавычка;
	КонецЕсли;

	Возврат Код;

КонецФункции

Процедура ВывестиУстановкуТелаЗапросаТекстовымиДанными()

	Если ОписаниеЗапроса.ПередаватьОтправляемыеДанныеВСтрокуЗапроса Тогда
		Возврат;
	КонецЕсли;

	ЭлементыТелаЗапросаДляВывода = Новый Массив;

	ОтправляемаяСтрока = СтрСоединить(ОписаниеЗапроса.ОтправляемыеТекстовыеДанные, "&");
	Если ЗначениеЗаполнено(ОтправляемаяСтрока) Тогда
		ЭлементыТелаЗапросаДляВывода.Добавить(ОбернутьКавычками(ОтправляемаяСтрока));
	КонецЕсли;

	Для Каждого ПрочитанныйФайл Из ПрочитанныеФайлы Цикл
		Если ПрочитанныйФайл.ПередаваемыйФайл.Назначение = НазначенияФайлов.ТелоЗапроса Тогда
			ЭлементыТелаЗапросаДляВывода.Добавить(ПрочитанныйФайл.ИмяПеременной);
		КонецЕсли;
	КонецЦикла;

	КоличествоЭлементов = ЭлементыТелаЗапросаДляВывода.Количество();
	Если КоличествоЭлементов = 1 Тогда
		ВывестиСтроку(СтрШаблон("%1.УстановитьТелоИзСтроки(%2);",
			ИмяПараметраHTTPЗапрос,
			ЭлементыТелаЗапросаДляВывода[0]));
	ИначеЕсли КоличествоЭлементов > 1 Тогда
		Разделитель = "
		|	+ ""&"" + ";

		ВывестиПустуюСтроку();
		ВывестиСтроку(СтрШаблон("ТелоЗапроса = %1;", СтрСоединить(ЭлементыТелаЗапросаДляВывода, Разделитель)));
		ВывестиПустуюСтроку();
		ВывестиСтроку(СтрШаблон("%1.УстановитьТелоИзСтроки(ТелоЗапроса);", ИмяПараметраHTTPЗапрос));
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиУстановкуТелаЗапросаДвоичнымиДанными()

	Если ОписаниеЗапроса.ПередаватьОтправляемыеДанныеВСтрокуЗапроса Тогда
		Возврат;
	КонецЕсли;

	ЭтоПервыйФайл = Истина;
	Для Каждого ПередаваемыйФайл Из ОписаниеЗапроса.Файлы Цикл

		Если ПередаваемыйФайл.ПрочитатьСодержимое 
			Или Не ПередаваемыйФайл.Назначение = НазначенияФайлов.ТелоЗапроса Тогда
			Продолжить;
		КонецЕсли;

		ВывестиСтроку(СтрШаблон("%1%2.УстановитьИмяФайлаТела(%3);", 
			?(ЭтоПервыйФайл, "", "// "),
			ИмяПараметраHTTPЗапрос, 
			ОбернутьКавычками(ПередаваемыйФайл.ИмяФайла)));

		ЭтоПервыйФайл = Ложь;

	КонецЦикла;

КонецПроцедуры

Процедура ВывестиВызовHTTPМетода()

	ИмяВыходногоФайла = "";

	ПараметрыФункции = Новый Массив;
	ПараметрыФункции.Добавить(ОбернутьКавычками(ОписаниеЗапроса.Метод));
	ПараметрыФункции.Добавить(ИмяПараметраHTTPЗапрос);

	Если ЗначениеЗаполнено(ИмяВыходногоФайла) Тогда
		ПараметрыФункции.Добавить(ИмяВыходногоФайла);
	КонецЕсли;

	ВывестиСтроку(СтрШаблон("%1 = %2.ВызватьHTTPМетод(%3);", 
		ИмяПараметраHTTPОтвет,
		ИмяПараметраHTTPСоединение,
		ПараметрыФункцииВСтроку(ПараметрыФункции)));

КонецПроцедуры

Функция СобратьИсходнуюСтрокуЗапроса(СтруктураURL)

	СтрокаЗапроса = "";

	Для Каждого Параметр Из СтруктураURL.ПараметрыЗапроса Цикл
		СтрокаЗапроса = СтрокаЗапроса 
			+ ?(СтрокаЗапроса = "", "", "&")
			+ Параметр.Ключ
			+ "="
			+ КодироватьСтроку(Параметр.Значение, СпособКодированияСтроки.URLВКодировкеURL);
	КонецЦикла;

	Возврат СтрокаЗапроса;

КонецФункции

Процедура ВывестиСтроку(Строка)
	Результат = Результат + Символы.ПС + Строка;
КонецПроцедуры

Процедура ВывестиПустуюСтроку()
	Результат = Результат + Символы.ПС;
КонецПроцедуры

Функция ПараметрыФункцииВСтроку(ПараметрыФункции)
	
	ПараметрыСтрокой = "";

	МаксимальнаяДлинаВсехПараметровНаОднойСтроке = 110;
	ПереноситьКаждыйПараметр = СтрДлина(СтрСоединить(ПараметрыФункции, ", ")) > МаксимальнаяДлинаВсехПараметровНаОднойСтроке;

	Если ПереноситьКаждыйПараметр Тогда
		Разделитель = "," + Символы.ПС + Символы.Таб;
	Иначе
		Разделитель = ", ";
	КонецЕсли;

	КоличествоПараметров = ПараметрыФункции.Количество();

	ЕстьЗаполненный = Ложь;
	Для Инд = 1 По КоличествоПараметров Цикл
		
		Значение = ПараметрыФункции[КоличествоПараметров - Инд];
		Если Не ЕстьЗаполненный И Не ЗначениеЗаполнено(Значение) Тогда
			Продолжить;
		КонецЕсли;

		ПараметрыСтрокой = Значение + ?(ПараметрыСтрокой = "", "", Разделитель) + ПараметрыСтрокой;

		ЕстьЗаполненный = Истина;

	КонецЦикла;

	Возврат СокрЛП(ПараметрыСтрокой);

КонецФункции

Функция ОбернутьКавычками(Значение, ОборачиватьТолькоЗаполненные = Ложь)
	Если ЗначениеЗаполнено(Значение) Или Не ОборачиватьТолькоЗаполненные Тогда
		Возврат СтрШаблон("""%1""", ЭкранироватьКавычки(Значение));
	КонецЕсли;
	Возврат "";
КонецФункции

Функция ЭкранироватьКавычки(Строка)
	Возврат СтрЗаменить(Строка, """", """""");
КонецФункции