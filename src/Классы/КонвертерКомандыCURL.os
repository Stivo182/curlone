#Использовать logos
#Использовать cli

Перем Лог;
Перем Конструктор;

#Область ПрограммныйИнтерфейс

Функция Конвертировать(КоманднаяСтрока, пКонструктор = Неопределено) Экспорт
	
	Если пКонструктор = Неопределено Тогда
		Конструктор = Новый КонстуркторПрограммногоКода1С();
	Иначе
		Конструктор = пКонструктор;
	КонецЕсли;

	Парсер = Новый ПарсерКонсольнойКоманды();
	АргументыКоманд = Парсер.Распарсить(КоманднаяСтрока);

	Приложение = Новый КонсольноеПриложение("curl", "", ЭтотОбъект);
	Приложение.УстановитьСпек("[URL...] [OPTIONS] [URL...]");

	Приложение.Аргумент("URL", "", "Адрес ресурса").ТМассивСтрок();
	Приложение.Опция("H header", "", "HTTP заголовок").ТМассивСтрок();
	Приложение.Опция("X request", "", "Метод запроса").ТСтрока();
	
	Приложение.УстановитьОсновноеДействие(ЭтотОбъект);

	Если АргументыКоманд.Количество() = 0 Тогда
		ВызватьИсключение "Команда должна начинаться с ""curl""";
	КонецЕсли;

	Для Каждого АргументыКоманды Из АргументыКоманд Цикл
		Если Не (НРег(АргументыКоманды[0]) = "curl") Тогда
			ВызватьИсключение "Команда должна начинаться с ""curl""";
		КонецЕсли;
		АргументыКоманды.Удалить(0);
		Приложение.Запустить(АргументыКоманды);
	КонецЦикла;

	Возврат Конструктор.Собрать();

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьКоманду(Команда) Экспорт

	ПередатьURL(Команда);
	ПередатьМетодЗапроса(Команда);
	ПередатьЗаголовки(Команда);

КонецПроцедуры

Процедура ПередатьМетодЗапроса(Команда)
	Метод = Команда.ЗначениеОпции("X");
	Если ЗначениеЗаполнено(Метод) Тогда
		Конструктор.УстановитьМетод(Метод);
	КонецЕсли;
КонецПроцедуры

Процедура ПередатьURL(Команда)
    МассивURL = Команда.ЗначениеАргумента("URL");
	Для Каждого АдресРесурса Из МассивURL Цикл
		Конструктор.ДобавитьURL(АдресРесурса);
	КонецЦикла;	
КонецПроцедуры

Процедура ПередатьЗаголовки(Команда)
	Конструктор.УстановитьЗаголовки(РазобратьЗаголовки(Команда));
КонецПроцедуры

Функция РазобратьЗаголовки(Команда)

	Заголовки = Новый Соответствие;
	МассивЗаголовков = Команда.ЗначениеОпции("H");
	Для Каждого Строка Из МассивЗаголовков Цикл
		Имя = "";
		Значение = "";

		ПозицияДвоеточия = СтрНайти(Строка, ":");
		Если ПозицияДвоеточия Тогда
			Имя = СокрЛП(Сред(Строка, 1, ПозицияДвоеточия - 1));
			Значение = СокрЛП(Сред(Строка, ПозицияДвоеточия + 1));
		Иначе
			Имя = Строка;
		КонецЕсли;

		Заголовки.Вставить(Имя, Значение);
	КонецЦикла;

	Возврат Заголовки;

КонецФункции

Процедура Инит()

	Лог = Логирование.ПолучитьЛог("oscript.lib.curlto1c");
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

Инит();

#КонецОбласти