#Использовать "../src/core"

Функция ПолучитьВсеОпции()

	Соединение = Новый HTTPСоединение("curl.se");
	Запрос = Новый HTTPЗапрос("docs/manpage.html");

	Ответ = Соединение.Получить(Запрос);
	ПрверяемаяСтрока = Ответ.ПолучитьТелоКакСтроку();

	РегулярноеВыражение = Новый РегулярноеВыражение("<p class=""level0""><a name=""(-+[a-zA-Z0-9]+[a-zA-Z0-9-.]*)""></a><span class=""nroffip"">([a-zA-Z0-9-,.\s]*)(.*)</span>");
	Совпадения = РегулярноеВыражение.НайтиСовпадения(ПрверяемаяСтрока);

	МассивОпций = Новый Массив();
	Для Каждого Совпадение Из Совпадения Цикл		
		Опции = СокрЛП(СтрЗаменить(Совпадение.Группы[2].Значение, ",", ""));

		КонецСтроки = Совпадение.Группы[3].Значение;
		ЭтоБулево = Не (СтрНайти(КонецСтроки, "&lt;") И СтрНайти(КонецСтроки, "&gt;"));

		МассивОпций.Добавить(Новый Структура("Опции, ЭтоБулево", Опции, ЭтоБулево));	
	КонецЦикла;

	Возврат МассивОпций;

КонецФункции

Функция ПодготовитьКодИнициализацииНеподдерживаемыхОпций()

	Опции = ПолучитьВсеОпции();

	Результат = Новый ТекстовыйДокумент();

	КонвертерКомандыCURL = Новый КонвертерКомандыCURL();
	ПоддерживаемыеОпции = КонвертерКомандыCURL.ПоддерживаемыеОпции();

	ПоддерживаемыеОпцииСоответствие = Новый Соответствие();
	Для Каждого ИмяОпции Из ПоддерживаемыеОпции Цикл
		ПоддерживаемыеОпцииСоответствие.Вставить(ИмяОпции, Истина);
	КонецЦикла;

	Для Каждого Строка Из Опции Цикл
		ОпцииБезОбработки = СтрРазделить(Строка.Опции, " ");
		Если ПоддерживаемыеОпцииСоответствие.Получить(ОпцияБезТире(ОпцииБезОбработки[0])) = Неопределено Тогда
			ОпцииОбработанные = Новый Массив();
			Для Каждого Опция Из ОпцииБезОбработки Цикл
				Опция = ОпцияБезТире(Опция);
				ОпцииОбработанные.Добавить(Опция);
			КонецЦикла;
			Текст = СтрШаблон("ДобавитьНеподдерживаемуюОпцию(""%1"").%2();", 
				СтрСоединить(ОпцииОбработанные, " "),
				?(Строка.ЭтоБулево, "ТМассивБулево", "ТМассивСтрок"));
			Результат.ДобавитьСтроку(Текст);
		КонецЕсли;
	КонецЦикла;

	Возврат Результат.ПолучитьТекст();

КонецФункции

Функция ОпцияБезТире(Наименование)
	ПозицияНаименования = ?(Лев(Наименование, 2) = "--", 3, 2);
	Опция = Сред(Наименование, ПозицияНаименования);
	Возврат Опция;	
КонецФункции

ПутьКФайлу = ТекущийКаталог() + ПолучитьРазделительПути() + "unsupported-options.txt";

Результат = Новый ТекстовыйДокумент();
Результат.УстановитьТекст(ПодготовитьКодИнициализацииНеподдерживаемыхОпций());
Результат.Записать(ПутьКФайлу);

Сообщить(ПутьКФайлу);