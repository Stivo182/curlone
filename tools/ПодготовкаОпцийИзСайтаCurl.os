#Использовать "../src"

Функция ПолучитьВсеОпции()

	Соединение = Новый HTTPСоединение("curl.se");
	Запрос = Новый HTTPЗапрос("docs/manpage.html");

	Ответ = Соединение.Получить(Запрос);
	ПрверяемаяСтрока = Ответ.ПолучитьТелоКакСтроку();

	РегулярноеВыражение = Новый РегулярноеВыражение("<a name=""(-+[a-zA-Z]+[a-zA-Z0-9-]*)""></a><span class=""nroffip"">([a-zA-Z0-9-,\s]*)(.*)</span>");
	Совпадения = РегулярноеВыражение.НайтиСовпадения(ПрверяемаяСтрока);

	МассивОпций = Новый Массив();
	Для Каждого Совпадение Из Совпадения Цикл		
		Опции = СокрЛП(СтрЗаменить(Совпадение.Группы[2].Значение, ",", ""));
		МассивОпций.Добавить(Опции);	
	КонецЦикла;

	Возврат МассивОпций;

КонецФункции

Функция ПодготовитьКодИнициализацииНеподдерживаемыхОпций()

	Опции = ПолучитьВсеОпции();

	Результат = Новый ТекстовыйДокумент();

	КонвертерКомандыCURL = Новый КонвертерКомандыCURL();
	ПоддерживаемыеОпции = КонвертерКомандыCURL.ПоддерживаемыеОпции();
	ПоддерживаемыеОпцииСоответствие = Новый Соответствие();

	Для Каждого Опция Из ПоддерживаемыеОпции Цикл
		ПоддерживаемыеОпцииСоответствие.Вставить(Опция, Истина);
	КонецЦикла;

	Для Каждого ГруппаОпций Из Опции Цикл
		ПерваяОпция = СтрРазделить(ГруппаОпций, " ")[0];
		Если ПоддерживаемыеОпцииСоответствие.Получить(ПерваяОпция) = Неопределено Тогда
			Результат.ДобавитьСтроку("Приложение.Опция(""" + ГруппаОпций + """).ТМассивСтрок();");
		КонецЕсли;
	КонецЦикла;

	Возврат Результат.ПолучитьТекст();

КонецФункции

ПутьКФайлу = ТекущийКаталог() + ПолучитьРазделительПути() + "unsupported-options.txt";

Результат = Новый ТекстовыйДокумент();
Результат.УстановитьТекст(ПодготовитьКодИнициализацииНеподдерживаемыхОпций());
Результат.Записать(ПутьКФайлу);

Сообщить(ПутьКФайлу);