#Использовать logos
#Использовать asserts
#Использовать ".."

Перем юТест;

Функция ПолучитьСписокТестов(Знач Тестирование) Экспорт

	юТест = Тестирование;
	
	СписокТестов = Новый Массив;
	
	СписокТестов.Добавить("ТестДолжен_ПолучитьПрограммныйКод1С");
	СписокТестов.Добавить("ТестДолжен_ВыброситьИсключениеКомандаНачинаетсяНеСCurl");
	СписокТестов.Добавить("ТестДолжен_ВыброситьИсключениеЕслиПереданаПустаяКоманда");

	Возврат СписокТестов;
	
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.curlto1c");
	Лог.УстановитьУровень(УровниЛога.Информация);
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
	юТест.УдалитьВременныеФайлы();
	Лог = Неопределено;

КонецПроцедуры

Процедура ТестДолжен_ВыброситьИсключениеКомандаНачинаетсяНеСCurl() Экспорт

	КонсольнаяКоманда = "myapp -H 'accept: text/html'";

	КонвертерКомандыCURL = Новый КонвертерКомандыCURL();
	
	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить(КонсольнаяКоманда);
	ПараметрыМетода.Добавить(Новый КонстуркторПрограммногоКода1С());

	Ожидаем.Что(КонвертерКомандыCURL)
		.Метод("Конвертировать", ПараметрыМетода)
		.ВыбрасываетИсключение("Команда должна начинаться с ""curl""");

КонецПроцедуры

Процедура ТестДолжен_ПолучитьПрограммныйКод1С() Экспорт

	КонсольнаяКоманда = "curl 'https://example.com?param1=value1' \
	|  -H 'accept: text/html' \
	|  -H 'accept-language: ru,en-US;q=0.9,en;q=0.8' \
	|  -H 'user-agent: curl'";

	ПрограммныйКод = "Заголовки = Новый Соответствие();
	|Заголовки.Вставить(""accept"", ""text/html"");
	|Заголовки.Вставить(""accept-language"", ""ru,en-US;q=0.9,en;q=0.8"");
	|Заголовки.Вставить(""user-agent"", ""curl"");
	|
	|ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
	|Соединение = Новый HTTPСоединение(""example.com"",
	|	443,
	|	,
	|	,
	|	,
	|	,
	|	ЗащищенноеСоединение);
	|
	|HTTPЗапрос = Новый HTTPЗапрос(""?param1=value1"", Заголовки);
	|HTTPОтвет = Соединение.ВызватьHTTPМетод(""GET"", HTTPЗапрос);";

	КонвертерКомандыCURL = Новый КонвертерКомандыCURL();
	Результат = КонвертерКомандыCURL.Конвертировать(КонсольнаяКоманда, Новый КонстуркторПрограммногоКода1С());

	Ожидаем.Что(Результат).Равно(ПрограммныйКод);
	
КонецПроцедуры

Процедура ТестДолжен_ВыброситьИсключениеЕслиПереданаПустаяКоманда() Экспорт

	КонсольнаяКоманда = "";

	КонвертерКомандыCURL = Новый КонвертерКомандыCURL();
	
	ПараметрыМетода = Новый Массив;
	ПараметрыМетода.Добавить(КонсольнаяКоманда);
	ПараметрыМетода.Добавить(Новый КонстуркторПрограммногоКода1С());

	Ожидаем.Что(КонвертерКомандыCURL)
		.Метод("Конвертировать", ПараметрыМетода)
		.ВыбрасываетИсключение("Команда должна начинаться с ""curl""");

КонецПроцедуры
